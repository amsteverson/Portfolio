/**********************************************************************************

DESCRIPTION:  THIS PROGRAM PROVIDES SUMMARY STATISTICS AND HYPOTHESIS TESTING 
			  FOR AGE OF DIAGNOSIS

              THE CONDITION USED IN THIS EXERCISE IS DIABETES (CCS CODE=049 OR 050)

INPUT FILES:  1) /folders/myfolders/sasuser.v94/MEPS.H180.SAS7BDAT (2015 CONDITION PUF DATA)
              2) /folders/myfolders/sasuser.v94/MEPS.H181.SAS7BDAT (2015 FY PUF DATA)

*********************************************************************************/;
OPTIONS LS=132 PS=79 NODATE FORMCHAR="|----|+|---+=|-/\<>*" PAGENO=1;
FILENAME MYLOG "/folders/myfolders/MIS 500 Portfolio Project/Age_Summary.TXT";
FILENAME MYPRINT "/folders/myfolders/MIS 500 Portfolio Project/Age_Summary.TXT";
PROC PRINTTO LOG=MYLOG PRINT=MYPRINT NEW;
RUN;

TITLE1 'MIS 500 PORTFOLIO PROJECT - SUMMARY STATISTICS AND HYPOTHESIS TESTS ORIGINAL CODE';
TITLE2 'CALCULATE SUMMARY STATISTICS ON AGE FOR PERSONS WITH A CONDITION (DIABETES)';


PROC FORMAT;

/* CODE FROM MEPS SAS SITE TO PROVIDE USER FRIENDLY LABELS ON VARIABLES*/
  VALUE SEX
     . = 'TOTAL'
     1 = 'MALE'
     2 = 'FEMALE'
       ;

  VALUE YESNO
     . = 'TOTAL'
     1 = 'YES'
     2 = 'NO'
       ;
/* ADDED FORMAT FOR RACE TO PROVIDE USER FRIENDLY LABELS ON RACES*/
  VALUE RACE
  	. = 'TOTAL'
  	1 = 'HISPANIC'
  	2 = 'NON-HISPANIC WHITE ONLY'
  	3 = 'NON-HISPANIC BLACK ONLY'
  	4 = 'NON-HISPANIC ASIAN ONLY'
  	5 = 'NON-HISPANIC OTHER RACE OR MULTIPLE RACE'
  		;
RUN;


/*1) MEPS PROVIDED CODE TO PULL OUT CONDITIONS WITH DIABETES (CCS CODE='049', '050') FROM 2015 CONDITION PUF - HC180*/

DATA DIAB;
 SET MEPS.H180;
    IF CCCODEX IN ('049', '050');
RUN;

TITLE3 "CHECK CCS CODES FOR DIABETIC CONDITIONS";
PROC FREQ DATA=DIAB;
  TABLES CCCODEX / LIST MISSING;
RUN;


/*2) MEPS PROVIDED CODE TO IDENTIFY PERSONS WHO REPORTED DIABETES*/

PROC SORT DATA=DIAB OUT=DIABPERS (KEEP=DUPERSID) NODUPKEY;
  BY DUPERSID;
RUN;


/*3) MEPS PROVIDED CODE TO CREATE A FLAG FOR PERSONS WITH DIABETES IN THE 2015 FY DATA*/

DATA  FY1;
MERGE MEPS.H181 (IN=AA)
      DIABPERS   (IN=BB) ;
   BY DUPERSID;

      LABEL DIABPERS='PERSONS WHO REPORTED DIABETES';
      IF AA AND BB THEN DIABPERS = 1;
                   ELSE DIABPERS = 2;

RUN;
TITLE3 "Supporting crosstabs for the flag variables";
TITLE3 "UNWEIGHTED # OF PERSONS WHO REPORTED DIABETES, 2015";
PROC FREQ DATA=FY1;
  TABLES DIABPERS
         SEX * DIABPERS / LIST MISSING;
  FORMAT SEX      sex.
         DIABPERS yesno.
    ;
RUN;

/*4) MEPS PROVIDED AND STUDENT MODIFIED CODE TO CALCULATE EXPENDITURES FOR PERSONS WITH DIABETES VS. NOT BY GENDER AND RACE*/

ODS GRAPHICS OFF;
ODS LISTING CLOSE;
PROC MEANS DATA=FY1 SUM STD MEAN;
	CLASS DIABPERS SEX;
	VAR TOTEXP15 TOTSLF15 OBTOTV15;
    FORMAT DIABPERS yesno.
    		SEX sex.
    		;
RUN;

ODS GRAPHICS OFF;
ODS LISTING CLOSE;
PROC MEANS DATA=FY1 SUM STD MEAN;
	CLASS DIABPERS RACETHX;
	VAR TOTEXP15 TOTSLF15 OBTOTV15;
    FORMAT DIABPERS yesno.
    		RACETHX race.
    		;
RUN;


/*5) STUDENT CREATED SUMMARY STATISTICS TO CALCULATE AGE OF DIAGNOSIS FOR PERSONS WHO REPORTED DIABETES BY RACE*/

PROC means data = FY1 (WHERE=(DIABPERS = 1 and diabaged > 0)) N STD MEAN MEDIAN;
	CLASS RACETHX;
	VAR DIABAGED ;
	FORMAT RACETHX	race.;
	RUN;
	
TITLE3 "UNWEIGHTED # OF PERSONS BY RACE, 2015";
PROC FREQ DATA=FY1;
  TABLES RACETHX / LIST MISSING;
  FORMAT RACETHX      race.
    ;
RUN;
TITLE3 "UNWEIGHTED # OF PERSONS WHO REPORTED DIABETES BY RACE, 2015";
PROC FREQ DATA=FY1 (WHERE = (DIABPERS = 1));
  TABLES RACETHX / LIST MISSING;
  FORMAT RACETHX      race.
    ;
RUN;
	
/*6) STUDENT CREATED TTESTS TO DETERMINE IF THERE IS A DIFFERENCE IN THE AGE OF DIABETES DIAGNOSIS OR SELF EXPENDITURES BY GENDER*/

PROC TTEST DATA = FY1 (WHERE=(DIABPERS = 1 and diabaged > 0));
	CLASS SEX;
	VAR DIABAGED;
	FORMAT SEX	sex.;
	Title 'H0: There is no difference between the average age of diabetes diagnosis for males and females';
RUN;

PROC TTEST DATA = FY1 (WHERE=(DIABPERS = 1 and totslf15 >= 0));
	CLASS SEX;
	VAR TOTSLF15;
	FORMAT SEX	sex.;
	Title 'H0: There is no difference between the average self paid expenses for males and females';
RUN;

/*6) STUDENT CREATED ANOVA TEST TO DETERMINE IF THERE IS A DIFFERENCE IN THE AGE OF DIABETES DIAGNOSIS OR SELF EXPENDITURES BY RACE*/

PROC ANOVA DATA = FY1 (WHERE=(DIABPERS = 1 and diabaged > 0));
	CLASS RACETHX;
	MODEL DIABAGED=RACETHX;
	MEANS RACETHX/TUKEY;
	FORMAT RACETHX race.;
TITLE 'H0: Average age of diabetes diagnosis are equal across races';
RUN;	

PROC ANOVA DATA = FY1 (WHERE=(DIABPERS = 1 and totslf15 >= 0));
	CLASS RACETHX;
	MODEL TOTSLF15=RACETHX;
	MEANS RACETHX/TUKEY;
	FORMAT RACETHX race.;
TITLE 'H0: Average self expenditures for diabetes diagnosed are equal across races';
RUN;
PROC PRINTTO;
RUN;	